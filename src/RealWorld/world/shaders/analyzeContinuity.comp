/*!
 *  @author     Dubsky Tomas
 */
#version 460
#include <RealWorld/constants/tile.glsl>
const int ActiveChunksSB_BINDING = 1;
#include <RealWorld/world/shaders/ActiveChunksSB.glsl>

layout (local_size_x = 8,
        local_size_y = 8,
        local_size_z = 1
) in;

int maxNumberOfUpdateChunks = b_worldTexSizeMask.x * b_worldTexSizeMask.y;

ivec2 activeChunkAtPos(ivec2 pos){
    return b_offsets[maxNumberOfUpdateChunks + pos.y * b_worldTexSizeCh.x + pos.x];
}

void main(){
    //Gather chunks
    const ivec2 posCh = ivec2(gl_GlobalInvocationID.xy);
    ivec2 ch00 = activeChunkAtPos(posCh);
    ivec2 ch01 = activeChunkAtPos((posCh + ivec2(0, 1)) & b_worldTexSizeMask);
    ivec2 ch10 = activeChunkAtPos((posCh + ivec2(1, 0)) & b_worldTexSizeMask);
    ivec2 ch11 = activeChunkAtPos((posCh + ivec2(1, 1)) & b_worldTexSizeMask);
    
    //Test their continuity
    if ((ch00 + ivec2(0, 1)) == ch01 &&
        (ch10 + ivec2(0, 1)) == ch11 && 
        (ch00 + ivec2(1, 0)) == ch10){
        //The update chunk is continous
        int i = atomicAdd(b_dynamicsGroupSize.x, 1);
        b_offsets[i] = posCh * iChunkTi + iChunkTi / 2;
    }
}
