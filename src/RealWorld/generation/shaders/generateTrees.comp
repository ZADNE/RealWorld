/*!
 *  @author     Dubsky Tomas
 */
#version 460
#include <RealWorld/constants/body.glsl>
#include <RealWorld/constants/tree.glsl>
const int TreeTemplatesUB_BINDING = 3;
#include <RealWorld/generation/shaders/TreeTemplatesUB.glsl>
const int BranchesSBWrite_BINDING = 4;
const int BranchesSBRead_BINDING = 5;
#include <RealWorld/trees/shaders/BranchesSB.glsl>
#include <RealWorld/generation/shaders/generateStructure.glsl>

const float k_piHalf = 1.57079632679;
const float k_piQuarter = 0.785398163;

layout( local_size_x = 64,
        local_size_y = 1,
        local_size_z = 1
) in;

shared uint rootIndex;

void main(){
    // Calculate the horizon
    float pPx = float(p_chunkOffsetTi.x) * TilePx.x;
    vec2 biomeClimate = biomeClimate(pPx, p_seed);
    Biome biome = biomeStructure(biomeClimate);
    float horizonPx = horizon(pPx, biome, p_seed).r;
    int horizonTi = int(pxToTi(horizonPx));
    if (horizonTi < p_chunkOffsetTi.y || horizonTi >= (p_chunkOffsetTi.y + iChunkTi.y)) {
        // The chunk is not on the horizon -> do not generate any trees
        return;
    }

    if (gl_LocalInvocationIndex == 0){
        uint rootIndex = atomicAdd(b_branchesHeaderWrite.vertexCount, gl_WorkGroupSize.x);
        if ((rootIndex + gl_WorkGroupSize.x) > b_branchesHeaderRead.maxBranchCount) {
            rootIndex = ~0;
            atomicAdd(b_branchesHeaderWrite.vertexCount, uint(-gl_WorkGroupSize.x));
        }
    }
    barrier();
    if (rootIndex == ~0) return;

    uint writeIndex = rootIndex + gl_LocalInvocationIndex;
    Branch tmplt = b_treeTemplateBranches[gl_LocalInvocationIndex];
    b_branchesWrite[writeIndex] = Branch(
        pxToTi(vec2(pPx, horizonPx)) + tmplt.absPosTi,
        rootIndex + tmplt.parentIndex,
        tmplt.angles,
        tmplt.radiusTi,
        tmplt.lengthTi,
        tmplt.density,
        tmplt.stiffness
    );
}
