/*!
 *  @author     Dubsky Tomas
 */
#version 460
#include <RealWorld/world/shaders/random.glsl>
#include <RealWorld/generation/shaders/generateStructure.glsl>
#include <RealWorld/generation/shaders/vegetation.glsl>
const int BranchesSBWrite_BINDING = 4;
const int BranchesSBRead_BINDING = 5;
#include <RealWorld/vegetation/shaders/BranchesSB.glsl>
const int VegPreparationSB_BINDING = 6;
#define VegPreparationSB_ACCESS writeonly
#include <RealWorld/generation/shaders/VegPreparationSB.glsl>

const float k_piHalf = 1.57079632679;
const float k_piQuarter = 0.785398163;

layout (local_size_x = 64,
        local_size_y = 1,
        local_size_z = 1
) in;

const int k_invocOffsetTi = iChunkTi.x / int(gl_WorkGroupSize.x);
const int k_randomOffsetTi = k_invocOffsetTi - 1;

int horizonPosTi(){
    int basePosTi = p_chunkOffsetTi.x + int(gl_LocalInvocationID.x) * k_invocOffsetTi;
    int randomOffsetTi = int(gl_LocalInvocationID.x) & k_randomOffsetTi;
    return basePosTi + randomOffsetTi;
}

shared uint s_vegInstanceCount;

void main(){
    // Init shared vars
    if (gl_LocalInvocationID.x == 0){
        s_vegInstanceCount = 0;
    }
    barrier();

    g_randomState =
        uint(p_chunkOffsetTi.y << 16 | p_chunkOffsetTi.x) ^
        uint(gl_LocalInvocationID.x * 2713) ^
        uint(p_seed);
    int pTi = horizonPosTi();
    float pPx = tiToPx(pTi);
    vec2 biomeClimate = biomeClimate(pPx, p_seed);
    Biome biome = biomeStructure(biomeClimate);
    float horizonPx = horizon(pPx, biome, p_seed).r;
    int horizonTi = int(pxToTi(horizonPx));

    // If on the horizon
    if (horizonTi >= p_chunkOffsetTi.y && horizonTi < (p_chunkOffsetTi.y + iChunkTi.y)) {
        // Select vegetation to generate
        Vegetation veg = biomeVegetation(biomeClimate);
        uint templateIndex = 0;
        const uint vegBranchCount = k_vegTemplates[templateIndex].y;
        // If probability test succeeds
        if (randomFloat() < veg.distr.w){
            // If there is space for new branches in the branch buffer
            uint writeIndex = atomicAdd(b_branchesHeaderWrite.vertexCount, vegBranchCount);
            if ((writeIndex + vegBranchCount) <= b_branchesHeaderRead.maxBranchCount) {
                // Create instance to be generated
                float greenMass = biomeClimate.x + biomeClimate.y;
                b_vegInstances[atomicAdd(s_vegInstanceCount, 1)] = VegInstance(
                    templateIndex,
                    writeIndex,
                    random(),
                    0,
                    vec2(pTi, horizonTi),
                    greenMass * 0.25 + 0.5,
                    randomFloat() + 0.5
                );
            } else {
                // No space for new branches
                atomicAdd(b_branchesHeaderWrite.vertexCount, uint(-vegBranchCount));
            }
        }
    }

    barrier();
    if (gl_LocalInvocationIndex == 0){
        b_dispatchSize = uvec4(s_vegInstanceCount, 1, 1, 0);
    }
}
