/*!
 *  @author     Dubsky Tomas
 */
#version 460
#include <RealWorld/constants/tile.glsl>
#include <RealWorld/constants/generation.glsl>
#include <RealWorld/generation/shaders/GenerationPC.glsl>

layout (local_size_x = k_genGroupSize,
        local_size_y = k_genGroupSize,
        local_size_z = 1
) in;

layout (set = 0, binding = 0, rgba8ui) uniform restrict uimage2DArray          u_tilesImage;
layout (set = 0, binding = 1, rgba8ui) uniform restrict readonly uimage2DArray u_materialImage;

const ivec2 NEIGHBORHOOD[] = {
    {-1, +1},     {+0, +1},     {+1, +1},
    {-1, +0},                   {+1, +0},
    {-1, -1},     {+0, -1},     {+1, -1}
};

void main() {
    // Load previous value of the tile
    const uint loadLayer = tileImgLoadLayer();
    const ivec2 pTi = ivec2(gl_GlobalInvocationID.xy);
    uvec4 tile = imageLoad(u_tilesImage, ivec3(pTi, loadLayer));

    // Examine neighbors
    uint neighborsN = 0u;
    for (int i = 0; i < NEIGHBORHOOD.length(); i++){
        uint neighbor = imageLoad(u_tilesImage, ivec3(pTi + NEIGHBORHOOD[i], loadLayer)).BL_T;
        neighborsN += uint(!isAirBlock(neighbor));
    }

    // Determine new tile
    if (tile.BL_T == AIR_BL){
        if (neighborsN > p_edgeConsolidationPromote){
            // Surrounded air -> promote to material
            tile = uvec4(imageLoad(u_materialImage, ivec3(pTi, gl_WorkGroupID.z)).BL_T, tile.gba);
        }
    } else {
        if (neighborsN < p_edgeConsolidationReduce){
            // Lonely solid -> reduce to air
            tile = uvec4(AIR.BL_T, tile.gba);
        }
    }

    // Store the new tile
    imageStore(u_tilesImage, ivec3(pTi, tileImgStoreLayer()), tile);
}
