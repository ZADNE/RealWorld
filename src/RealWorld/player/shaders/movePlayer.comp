/*!
 *  @author     Dubsky Tomas
 */
#version 460
#include <RealWorld/constants/tile.glsl>
#include <RealWorld/player/shaders/playerPll.glsl>
#include <RealWorld/world/shaders/tileLoad.glsl>
#include <RealWorld/player/shaders/PlayerHitboxSB.glsl>
#include <RealWorld/player/shaders/PlayerMovementPC.glsl>

layout (local_size_x = 1,
        local_size_y = 1,
        local_size_z = 1
) in;

uint ground(vec2 posPx, vec2 dimsPx) {
    uint grnd = AIR.BLOCK_TYPE;
    int  yTi  = int(pxToTi(posPx.y - 1.0));
    for (int xTi = int(pxToTi(posPx.x)); xTi <= pxToTi(posPx.x + dimsPx.x); ++xTi) {
        grnd = min(grnd, tileLoad(ivec2(xTi, yTi)).BLOCK_TYPE);
    }
    return grnd;
}

bool overlapsBlocks(vec2 posPx, vec2 dimsPx) {
    for (int yTi = int(pxToTi(posPx.y)); yTi <= pxToTi(posPx.y + dimsPx.y); ++yTi) {
        for (int xTi = int(pxToTi(posPx.x)); xTi <= pxToTi(posPx.x + dimsPx.x); ++xTi) {
            if (isSolidBlock(tileLoad(ivec2(xTi, yTi)).BL_T)) {
                return true;
            }
        }
    }
    return false;
}

void tryJump(vec2 posPx, float jumpPower, vec2 dimsPx, inout vec2 velPx) {
    if (p_jump != 0.0 || // Jump
        (p_autojump != 0.0 && p_walkDirection != 0.0 &&
         overlapsBlocks(posPx + vec2(velPx.x * 4.0, 0.0), dimsPx))) { // Autojump
        velPx.y = p_jumpVelocity * jumpPower;
    }
}

void main() {
    const vec2 dimsPx = b_player.dimsPx;
          vec2 velPx = b_player.velocityPx;
    if (p_walkDirection != 0.0) {
        velPx.x += p_walkDirection * p_acceleration; // Walk
        velPx.x = clamp(velPx.x, -p_maxWalkVelocity, p_maxWalkVelocity);
    } else {
        velPx.x -= sign(velPx.x); // Friction
    }
    // Read previous botLeft from buffer
    vec2 botLeftPx = b_player.botLeftPx[1 - p_writeIndex];

    uint grnd = ground(botLeftPx, dimsPx);
    if (isSolidBlock(grnd)) { // Standing on solid ground
        tryJump(botLeftPx, 1.0, dimsPx, velPx);
    } else if (!isAirBlock(grnd)) { // Floating in a fluid
        tryJump(botLeftPx, 0.75, dimsPx, velPx);
        velPx *= 0.9375;
        velPx *= 0.9375;
        velPx.y -= 0.1875; // Gravity
    } else {                      // Falling through air
        velPx.y -= 0.1875; // Gravity
    }

    vec2 truncVelPx = trunc(velPx);
    if (overlapsBlocks(botLeftPx + vec2(velPx.x, 0.0), dimsPx)) {
        float velSign = sign(velPx.x);
        while (!overlapsBlocks(botLeftPx + vec2(velSign, 0.0), dimsPx)) {
            botLeftPx.x += velSign;
        }
        velPx.x = 0.0;
    } else {
        botLeftPx.x += truncVelPx.x;
    }

    if (overlapsBlocks(botLeftPx + vec2(0.0, velPx.y), dimsPx)) {
        float velSign = sign(velPx.y);
        while (!overlapsBlocks(botLeftPx + vec2(0.0, velSign), dimsPx)) {
            botLeftPx.y += velSign;
        }
        velPx.y = 0.0;
    } else {
        botLeftPx.y += truncVelPx.y;
    }

    // Write output back to the buffer
    b_player.botLeftPx[p_writeIndex] = botLeftPx;
    b_player.velocityPx = velPx;
}
